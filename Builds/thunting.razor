////////////////////////////////////////////////////////////////////////////////
// AFK TREASURE HUNTING SCRIPT
// written by karms
// last modified 28/12/2021

// REQUIREMENTS
//    - Lockpicks and Trap Tools in bank
//    - Locksmith Training Box in backpack

// SETUP
//    - Set up Restock 2 to grab LOCKPICKS
//    - Set up Restock 3 to grab TRAP TOOLS
//    - Stand at bank with bank box open
////////////////////////////////////////////////////////////////////////////////

@setvar! "havebox" 1
@setvar! "havepicks" 1
@setvar! "havedrills" 1

// required for hiding/stealth
if not timerexists "hide"
    createtimer "hide"
    settimer "hide" 11000
endif

while skill "Lockpicking" < 100 or skill "Remove Trap" < 100 or skill "Hiding" < 100 or skill "Stealth" < 100
    if skill "Hiding" < 100 or skill "Stealth" < 100
        if timer "hide" >= 11000
            if skill "Hiding" < 100
                useskill "Hiding"
            elseif hidden
                useskill "Stealth"
            else
                useskill "Hiding"
            endif
            settimer "hide" 0
        endif
    endif

    if skill "Lockpicking" < 100
        if findtype "strong box" backpack as "foundbox"
            @setvar! "havebox" 1
            if findtype "lockpicks" backpack
                @setvar! "havepicks" 1
                if gumpexists 736038070
                    gumpclose 736038070
                endif
                dclick "foundbox"
                waitforgump 736038070
                gumpresponse 4 736038070
                wait 4000
            elseif "havepicks" = 1
                restock 2
                wft 1000
                target "self"
                wait 2000
                if insysmsg "queued 0 items" or not findtype "lockpicks" backpack
                    sysmsg "Out of lockpicks or cannot access bank!" 34
                    @setvar! "havepicks" 0
                endif
            endif
        else
            @setvar! "havebox" 0
        endif
    endif

    if skill "Hiding" < 100 or skill "Stealth" < 100
        if timer "hide" >= 11000
            if skill "Hiding" < 100
                useskill "Hiding"
            elseif hidden
                useskill "Stealth"
            else
                useskill "Hiding"
            endif
            settimer "hide" 0
        endif
    endif

    if skill "Remove Trap" < 100
        if findtype "strong box" backpack as "foundbox"
            @setvar! "havebox" 1
            if findtype "drill" backpack
                @setvar! "havedrills" 1
                if gumpexists 736038070
                    gumpclose 736038070
                endif
                dclick "foundbox"
                waitforgump 736038070
                gumpresponse 7 736038070
                wait 4000
            elseif "havedrills" = 1
                restock 3
                wft 1000
                target "self"
                wait 2000
                if insysmsg "queued 0 items" or not findtype "drill" backpack
                    sysmsg "Out of trap tools or cannot access bank!" 34
                    @setvar! "havedrills" 0
                endif
            endif
        else
            @setvar! "havebox" 0
        endif
    endif

    if skill "Lockpicking" < 100 or skill "Remove Trap" < 100
        if skill "Hiding" = 100 and skill "Stealth" = 100
            if "havebox" = 0
                sysmsg "No lockbox!" 34
                break
            elseif "havepicks" = 0 and "havedrills" = 0
                sysmsg "Out of lock picks and trap tools!" 34
                break
            endif
        endif
    endif
endwhile